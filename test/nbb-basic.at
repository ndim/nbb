dnl ===================================================================

AT_COPYRIGHT([Copyright (C) 2008 Hans Ulrich Niedermann])
AT_TESTED([nbb])
AT_PYTHON_TO_PATH()dnl

dnl ===================================================================

AT_SETUP([nbb basic: --version option])
AT_KEYWORDS([nbb version])
AT_CHECK([AT_NBB --version],
[0],
[nbb (ndim's branch builder) AT_PACKAGE_VERSION
])
AT_CLEANUP()

AT_SETUP([nbb basic: --version package name consistency])
AT_KEYWORDS([nbb version])
AT_CHECK([AT_NBB --version | sed 's/^@<:@^(@:>@*(//; s/)@<:@^)@:>@*$//' | sed "s/'//"],
[0],
[AT_PACKAGE_NAME
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb basic: --help option])
AT_KEYWORDS([nbb help])
AT_CHECK([AT_NBB --help], [0], [ignore], [ignore])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb basic: no parameters at all])
AT_KEYWORDS([nbb no parameters])
AT_CHECK([AT_NBB], [2], [], 
[ERROR: Command line error: nbb requires some arguments
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb basic: unknown command])
AT_KEYWORDS([nbb unknown command])
AT_CHECK([AT_NBB this-command-does-not-exist], [2], [],
[ERROR: Unknown nbb command 'this-command-does-not-exist'
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb help: without params])
AT_KEYWORDS([nbb help])
AT_CHECK([AT_NBB help], [0], [ignore], [])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb help: command help])
AT_KEYWORDS([nbb help command])
AT_CHECK([dnl
sep='======================================================================='
echo "$sep"
for cmd in $(nbb help | sed -n 's/^\t\(@<:@^ \t@:>@\{1,\}\).*/\1/p')
do
    AT_NBB help "$cmd" || { s=$?; echo "Error running 'nbb help $cmd"; exit $s; }
    echo "$sep"
done], [0], [ignore], [])
AT_CLEANUP()

sed -n 's/^\t\([^ \t]\{1,\}\).*/\1/p'

dnl ===================================================================

AT_SETUP([nbb global option: --vcs=git])
AT_KEYWORDS([nbb vcs global])
AT_CHECK([AT_NBB --vcs=git --version], [0], [dnl
nbb (ndim's branch builder) AT_PACKAGE_VERSION
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb global option: --vcs git])
AT_KEYWORDS([nbb vcs global])
AT_CHECK([AT_NBB --vcs git --version], [0], [dnl
nbb (ndim's branch builder) AT_PACKAGE_VERSION
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb global option: -v git])
AT_KEYWORDS([nbb vcs global])
AT_CHECK([AT_NBB -v git --version], [0], [dnl
nbb (ndim's branch builder) AT_PACKAGE_VERSION
])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb global option: multiple -v git --vcs git])
AT_KEYWORDS([nbb vcs global])
AT_CHECK([AT_NBB -v git --vcs git --version], [1], [ignore], [ignore])
AT_CLEANUP()

dnl ===================================================================

AT_SETUP([nbb internal-config: show internal config])
AT_KEYWORDS([nbb internal-config])
AT_CHECK([AT_NBB internal-config], [0], [ignore], [])
AT_CLEANUP()

dnl ===================================================================

dnl Local Variables:
dnl mode: Autoconf
dnl End:
