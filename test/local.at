AT_COPYRIGHT([Copyright (C) 2007,2008 Hans Ulrich Niedermann])
AT_TESTED([autom4te autoconf autoheader autoupdate autoreconf])
# Local initialization of the test environment would happen here


dnl Run proper python interpreter
dnl AT_PYTHON_TO_PATH()dnl
m4_define([AT_PYTHON_TO_PATH], [dnl
AS_IF([test -x "$PYTHON"], [pybindir=`dirname "$PYTHON"`
AS_IF([test -d "$pybindir"], [dnl
ifs=$IFS
found=false
IFS=:
for p in $PATH
do
	IFS=$ifs
	if test "x$pybindir" = "x$p"
	then
		found=:
		break
	fi
done
IFS=$ifs
if $found; then
	PATH="$pybindir:$PATH"
	export PATH
fi
])dnl
])dnl
])dnl


dnl AT_NBB
m4_define([AT_NBB], [dnl
$(if test "${NBB_SCRIPT-nbb}" = "nbb"; then echo "nbb"; else echo $PYTHON $NBB_SCRIPT; fi)dnl
])dnl


dnl AT_GIT_INIT_OUTPUT_CANONICAL
m4_define([AT_GIT_INIT_OUTPUT_CANONICAL], [dnl
sed 's|^Initialized empty Git repository in @<:@a-zA-Z0-9/\._-@:>@\{1,\}.git/$|Initialized empty Git repository in .git/|' dnl
])dnl


dnl AT_WRAP_GIT([stuff-to-do-with-git-repo])dnl
m4_define([AT_WRAP_GIT], [dnl
AT_CHECK([mkdir test.dir && cd test.dir])
AT_CHECK([cd test.dir && git init | AT_GIT_INIT_OUTPUT_CANONICAL], [0],
[Initialized empty Git repository in .git/
])dnl
$1
AT_CHECK([rm -rf test.dir])dnl
])dnl


dnl AT_WRAP_GIT_SCONS([stuff-to-do-with-tree])dnl
m4_define([AT_WRAP_GIT_SCONS], [dnl
AT_WRAP_GIT([dnl
AT_DATA([test.dir/SConstruct], [dnl
# Test
env = Environment()
env.Configure()
prog = env.Program('foobar',
                   source=@<:@'foobar.c'@:>@)
env.Install('/usr/bin', prog)
env.Alias('install', '/usr/bin')
])
AT_DATA([test.dir/foobar.c], [dnl
#include <stdio.h>
int main(int argc, char *argv@<:@@:>@) {
    printf("FOOBAR!\n");
    return 0;
}
])dnl
$1
])dnl
])dnl


dnl AT_WRAP_GIT_AM([stuff-to-do-with-tree])dnl
m4_define([AT_WRAP_GIT_AM], [dnl
AT_WRAP_GIT([
AT_DATA([test.dir/configure.ac], [dnl
AC[_]INIT(nbb git/automake test, 1.2.3, invalid@invalid.invalid, nbb-git-am-test)
AC_CONFIG_SRCDIR([foobar.c])
AM[_]INIT_AUTOMAKE([-Wall foreign])
AC_PROG_CC
AC_PROG_INSTALL
AC[_]CONFIG_FILES(@<:@Makefile@:>@)
AC[_]OUTPUT
])dnl
AT_DATA([test.dir/Makefile.am], [dnl
TESTS = foobar
bin_PROGRAMS = foobar
foobar_SOURCES = foobar.c
])dnl
AT_DATA([test.dir/foobar.c], [dnl
#include <stdio.h>
int main(int argc, char *argv@<:@@:>@) {
    printf("FOOBAR!\n");
    return 0;
}
])dnl
$1
])dnl
])dnl
